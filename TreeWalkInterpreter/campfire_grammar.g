program -> declaration* EOF;
declaration -> classDecl | funcDecl | varDecl | statement;
classDecl -> "class" IDENTIFIER ("<" IDENTIFIER)? "{" funtion* "}";
funDecl -> "fun" function ;
function -> IDENTIFIER "(" parameters? ")" block ;
parameters -> IDENTIFIER ( "," IDENTIFIER )* ;
varDecl -> "var" IDENTIFIER ( "=" expression )? ";";
statement -> exprStatement | forStatement | ifStatement | printStatement | returnStatement | whileStatement | block;
exprStatement -> expression ";";
forStatement -> "for" "(" ( varDecl | exprStmt | ";" ) expression? ";" expression? ")" statement;
ifStatement -> "if" "(" expression ")" statement ( "else" statement )?;
printStatement -> "print" expression ";";
whileStatement -> "while" "(" expression ")" statement;
returnStatement -> "return" expression? ";" ;
block -> "{" declaration* "}";
expression -> assignment;
assignment -> ( call "." )? IDENTIFIER "=" assignment | logic_or;
logic_or -> logic_and ( "or" logic_and )*;
logic_and -> equality( "and" equality)*; 
equality -> comparison ( ( "!=" | "==" ) comparison )* ; 
comparison -> term ( ( ">" | ">=" | "<" | "<=" ) term)* ;
term -> factor ( ( "-" | "+" ) factor )* ;
factor -> unary ( ( "/" | "*" ) unary)*;
unary -> ( "!" | "-" ) unary | call ;
call -> primary ( "(" arguments? ")" | "." IDENTIFIER )* ;
arguments -> expression ( "," expression )* ;
primary -> literal | grouping | IDENTIFIER | "super" "." IDENTIFIER; 
literal -> NUMBER | STRING| "true" | "false" | "nil" ;
grouping -> "(" expression ")" ;